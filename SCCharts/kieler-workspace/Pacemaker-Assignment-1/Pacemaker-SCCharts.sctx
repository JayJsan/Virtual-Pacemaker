// Get timing.h
#resource "timing.h"
#hostcode-c "#include \"timing.h\""
@ClocksUseSD
scchart Pacemaker {
  // Get timing parameters from timing.h
  host "int" AVI = `AVI_VALUE`
  host "int" AEI = `AEI_VALUE`
  host "int" PVARP = `PVARP_VALUE`
  host "int" VRP = `VRP_VALUE`
  host "int" LRI = `LRI_VALUE`
  host "int" URI = `URI_VALUE`
  
  // === INPUTS === 
  // AS = Atrial Sense
  // VS = Ventricle Sense
  input signal AS,VS
    
  // === OUTPUTS ===
  // AP = Atrial Pace
  // VP = Ventricle Pace
  output signal AP,VP
  
  clock VRP_Clock = 0
  clock AEI_Clock = 0
  clock LRI_Clock = 0
  clock URI_Clock = 0
  
  region {
    clock PVARP_Clock = 0
    
    state WaitForPVARP
    if PVARP_Clock >= PVARP go to WaitForASense
    
    state WaitForASense
    if AS == 0 && AEI_Clock >= AEI do AP go to VSense
    if AS == 1 go to VSense
    
    initial state VSense {
        clock AVI_Clock = 0
        
      initial state WaitForURI {
        entry do AVI_Clock = 0
      }
      if URI_Clock >= URI go to WaitForVSense
      if VS == 1 && VRP_Clock >= VRP go to RestartVTimers
        
      state WaitForVSense 
      if VS == 0 && AVI_Clock >= AVI do VP go to RestartVTimers
      if VS == 1 && VRP_Clock >= VRP go to RestartVTimers
      if VS == 0 && LRI_Clock >= LRI do VP go to RestartVTimers
      
      final state RestartVTimers {
        entry do PVARP_Clock = 0; VRP_Clock = 0; AEI_Clock = 0; LRI_Clock = 0; URI_Clock = 0
      }      
    }
    join to WaitForPVARP
  }
}